apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.frogermcs.dagger2metrics'



android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        applicationId globalConfiguration.getAt("androidApplicationId")
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        testInstrumentationRunner globalConfiguration.getAt("testInstrumentationRunner")
        testApplicationId globalConfiguration.getAt("testApplicationId")

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
    }
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }
}


def daggerVersion = '2.0.2'
def rxJavaVersion = '1.0.14'
def supportLibVersion = '23.4.0'
def rxAndroidVersion = '1.0.1'
def javaxAnnotationVersion = '1.0'
def retrolambdaVersion = '2.0.6'

def mockitoVersion = '1.9.5'
def dexmakerVersion = '1.0'
def espressoVersion = '2.0'
def testingSupportLibVersion = '0.1'


def presentationDependencies = [
        daggerCompiler : "com.google.dagger:dagger-compiler:${daggerVersion}",
        dagger         : "com.google.dagger:dagger:${daggerVersion}",
        recyclerView   : "com.android.support:recyclerview-v7:${supportLibVersion}",
        rxJava         : "io.reactivex:rxjava:${rxJavaVersion}",
        rxAndroid      : "io.reactivex:rxandroid:${rxAndroidVersion}",
        javaxAnnotation: "javax.annotation:jsr250-api:${javaxAnnotationVersion}",
        cardView       : "com.android.support:cardview-v7:${supportLibVersion}",
        appcompat      : "com.android.support:appcompat-v7:${supportLibVersion}",
        design         : "com.android.support:design:${supportLibVersion}",
        lambda         : "net.orfjackal.retrolambda:retrolambda:${retrolambdaVersion}",
]
def presentationTestDependencies = [
        mockito          : "org.mockito:mockito-core:${mockitoVersion}",
        dexmaker         : "com.google.dexmaker:dexmaker:${dexmakerVersion}",
        dexmakerMockito  : "com.google.dexmaker:dexmaker-mockito:${dexmakerVersion}",
        espresso         : "com.android.support.test.espresso:espresso-core:${espressoVersion}",
        testingSupportLib: "com.android.support.test:testing-support-lib:${testingSupportLibVersion}",
]

dependencies {
    compile project(':data')
    compile project(':domain')

    apt presentationDependencies.daggerCompiler
    compile presentationDependencies.dagger

    retrolambdaConfig presentationDependencies.lambda

    compile presentationDependencies.recyclerView
    compile presentationDependencies.cardView
    compile presentationDependencies.appcompat
    compile presentationDependencies.design

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'

    compile presentationDependencies.rxJava
    compile presentationDependencies.rxAndroid
    provided presentationDependencies.javaxAnnotation

    androidTestCompile presentationTestDependencies.mockito
    androidTestCompile presentationTestDependencies.dexmaker
    androidTestCompile presentationTestDependencies.dexmakerMockito
    androidTestCompile presentationTestDependencies.espresso
    androidTestCompile presentationTestDependencies.testingSupportLib


    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha1'

}
